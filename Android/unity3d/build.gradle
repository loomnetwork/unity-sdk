import org.gradle.internal.os.OperatingSystem
import java.nio.file.Paths

apply plugin: 'com.android.library'

android {
    compileSdkVersion 26
    buildToolsVersion '26.0.2'
    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    productFlavors {
    }
}

// TODO: Linux & IL2CPP
def getUnityClassesJar(boolean debugBuild) {
    String installDir = System.getenv('UNITY3D_INSTALL_DIR');
    if (installDir == null) {
        throw new GradleException('UNITY3D_INSTALL_DIR env var must be set to location of Unity installation dir.')
    }
    if (OperatingSystem.current().isWindows()) {
        return Paths.get(installDir,
                "Editor\\Data\\PlaybackEngines\\AndroidPlayer\\Variations\\mono",
                debugBuild ? "Development" : "Release",
                "Classes\\classes.jar")
    } else if (OperatingSystem.current().isMacOsX()) {
        return Paths.get(installDir,
                "PlaybackEngines/AndroidPlayer/Variations/mono",
                debugBuild ? "Development" : "Release",
                "Classes/classes.jar")
    }
    throw new GradleException("Building the SDK on this platform is not supported yet.")
}

dependencies {
    implementation 'com.android.support:support-v4:26.1.0'
    implementation 'com.auth0.android:auth0:1.12.1'
    implementation 'com.google.code.gson:gson:2.8.2'
    // UnityAndroidPlayer classes that are needed to compile the plugin, but won't be packaged with
    // since they will be provided by Unity itself.
    debugCompileOnly files(getUnityClassesJar(true))
    releaseCompileOnly files(getUnityClassesJar(false))
    implementation 'com.android.support:appcompat-v7:26.1.0'
    implementation 'com.android.support:customtabs:26.1.0'

    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}

android.libraryVariants.all { variant ->
    // variant.name will be the name of the build config, e.g. release / debug
    String variantName = "${variant.name.capitalize()}";
    String deployTaskGroup = "unity3dPlugin";
    String deployTaskName = "deploy${variantName}Plugin";
    String dependencyTaskName = "assemble${variantName}";
    String srcAAR = "${project.name}-${variant.name}.aar";

    // Create a Gradle task to copy the compiled Android Library (aar) to ../Assets/Plugins/Android
    task(deployTaskName, dependsOn: dependencyTaskName, type: Copy) {
        from("${buildDir.getPath()}/outputs/aar/")
        into("${rootDir.getPath()}/../Assets/Plugins/Android")
        include(srcAAR)
        rename(srcAAR, "io.loomx.unity3d.aar")
    }.group = deployTaskGroup;
}
